# http://www.scons.org/doc/production/HTML/scons-user.html
# This is: apps/completer

import os
from os.path import join
Import('env', 'casm_lib', 'build_lib_paths')

if 'BASH_COMPLETION_DIR' in env:
  bash_completion_dir = env['BASH_COMPLETION_DIR']
  
  # include paths
  candidates = [
    env['INCDIR'], 
    env.include_path(env['BOOST_INCLUDEDIR'], 'boost')
  ]
  cpppath = [x for x in candidates if x is not None]

  complete_obj = env.Object('complete.cpp', 
                        CPPPATH = cpppath)
  Default(complete_obj)
  Export('complete_obj')
  env['COMPILE_TARGETS'] = env['COMPILE_TARGETS'] + [complete_obj]

  # Construct 'casm-complete' executable

  # link flags
  linkflags = env['LDFLAGS']
  if env['PLATFORM'] == 'darwin':
    linkflags = ['-rpath', '@loader_path/../lib']
  else:
    linkflags = ['-Wl,--rpath=\\$$ORIGIN/../lib', '-z', 'origin']

  casm_complete = env.Program(os.path.join(env['BINDIR'], 'casm-complete'),
                     [complete_obj],
                     CPPPATH=cpppath,
                     LIBPATH=build_lib_paths,
                     LINKFLAGS=linkflags,
                     LIBS=[
                       env['boost_system'], 
                       env['boost_filesystem'], 
                       env['boost_program_options'], 
                       env['boost_regex'], 
                       env['boost_chrono'], 
                       env['dl'], 
                       env['z'],
                       'casm'])
                     
  Depends(casm_complete, [casm_lib])
  env.Alias('casm_complete', casm_complete)
  Export('casm_complete')
  env['COMPILE_TARGETS'] = env['COMPILE_TARGETS'] + [casm_complete]

  # Install 'casm-complete' instructions
  casm_complete_install = env.Install(env['CASM_BINDIR'], casm_complete)
  Export('casm_complete_install')
  if 'casm_complete_install' in COMMAND_LINE_TARGETS:
      env['IS_INSTALL'] = 1

  casm_completion_script = join(os.getcwd(),'casm')
  casm_completion_script_install = env.Install(bash_completion_dir, casm_completion_script)
  env['INSTALL_TARGETS'] = env['INSTALL_TARGETS'] + [casm_completion_script_install]
  Export('casm_completion_script_install')
  
  env.Append(INSTALL_TARGETS = [casm_complete_install, casm_completion_script_install])

  # Install 'casm-complete' and 'casm' bash completion script
  env.Alias('casm_complete_install', [casm_complete_install, casm_completion_script_install])
  
