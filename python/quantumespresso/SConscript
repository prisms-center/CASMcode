# http://www.scons.org/doc/production/HTML/scons-user.html
# This is: python/quantumespresso

import os
from distutils.spawn import find_executable

Import('env', 'version_number', 'url')

pyquantumespresso_setup = env.Command('setup.py', ['make_setup.py'], 'cd python/quantumespresso && python make_setup.py ' + version_number + ' ' + url)
env.NoClean(pyquantumespresso_setup)

cmd = 'cd python/quantumespresso && ' + find_executable('python') + ' setup.py install'
cmd += ' --prefix=' + env['CASM_PYTHON_PREFIX']
cmd += ' --record installed.txt'

pyquantumespresso_install = env.Command('installed.txt', [pyquantumespresso_setup], cmd)
AlwaysBuild(pyquantumespresso_install)
env.Append(INSTALL_TARGETS = [pyquantumespresso_install])
Alias('pyquantumespresso_install', pyquantumespresso_install)  

# with setup.py, must uninstall files manually, 
#  so we store a file with the install location of the files
installed_files = []
if os.path.isfile('installed.txt'):
  installed_files = [line.rstrip('\n') for line in open('installed.txt')]

env.Clean('pyquantumespresso_install', installed_files + ['build', 'dist', 'quantumespresso.egg-info'])

if 'pyquantumespresso_install' in COMMAND_LINE_TARGETS:
  env['IS_INSTALL'] = 1

    
