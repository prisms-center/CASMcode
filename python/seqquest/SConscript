# http://www.scons.org/doc/production/HTML/scons-user.html
# This is: python/seqquest

import os
from distutils.spawn import find_executable

Import('env', 'version_number', 'url')

pyquest_setup = env.Command('setup.py', ['make_setup.py'], 'cd python/seqquest && python make_setup.py ' + version_number + ' ' + url)
env.NoClean(pyquest_setup)

cmd = 'cd python/seqquest && ' + find_executable('python') + ' setup.py install'
cmd += ' --prefix=' + env['CASM_PYTHON_PREFIX']
cmd += ' --record installed.txt'

pyquest_install = env.Command('installed.txt', [pyquest_setup], cmd)
AlwaysBuild(pyquest_install)
Alias('pyquest_install', pyquest_install)  

# with setup.py, must uninstall files manually, 
#  so we store a file with the install location of the files
installed_files = []
if os.path.isfile('installed.txt'):
  installed_files = [line.rstrip('\n') for line in open('installed.txt')]

env.Clean('pyquest_install', installed_files + ['build', 'dist', 'seqquest.egg-info'])

if 'pyquest_install' in COMMAND_LINE_TARGETS:
  env['IS_INSTALL'] = 1

    
