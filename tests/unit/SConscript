# http://www.scons.org/doc/production/HTML/scons-user.html
# This is: test/unit/SConscript

import os, glob

# Import dependencies
Import('env', 'casm_lib', 'casm', 'build_lib_paths')

lenv = env.Clone(CPPPATH = env['CPPPATH'] + [os.getcwd()])

common_obj = lenv.Object('Common.cpp')
unit_obj = lenv.Object('unit_test.cpp')
test_src = glob.glob('*/*_test.cpp')
test_name = [ os.path.splitext(os.path.basename(src))[0] for src in test_src]
test_obj = [lenv.Object(x) for x in test_src]

linkflags = ""
if lenv['PLATFORM'] == 'darwin':
  linkflags = ['-rpath', '@loader_path/../../../lib']
else:
  linkflags = ['-Wl,--rpath=\\$$ORIGIN/../../../lib', '-z', 'origin']

unit_test = lenv.Program(os.path.join(lenv['UNIT_TEST_BIN'],'unit_test'), 
                        [unit_obj, test_obj, common_obj],
                        LINKFLAGS=linkflags,
                        LIBPATH=build_lib_paths,
                        LIBS=['boost_unit_test_framework', 'boost_system', 'boost_filesystem', 'dl', 'casm'])
Depends(unit_test, [casm_lib, casm])

# Execute 'scons unit' to compile & run all unit tests
env.Alias('unit', unit_test, unit_test[0].abspath + " --log_level=test_suite --catch_system_errors=no")
AlwaysBuild(unit_test)

if 'unit' in COMMAND_LINE_TARGETS:
  env['IS_TEST'] = 1

Structure_out = glob.glob('crystallography/*_out') + ['crystallography/POS1_prim.json']
Clexulator_out = ['clex/test_Clexulator.o', 'clex/test_Clexulator.so']
App_out = ['App/FCC_ternary']

Clean(unit_test,  Structure_out + Clexulator_out + App_out)

for i, src_name in enumerate(test_name):
  if src_name[:-5] in ["Structure", "BasicStructure", "ConfigIODictionary", "App", 
                       "ConfigIO", "PrimClex", "ChemicalReference", "NeighborList"]:
    test = lenv.Program(os.path.join(lenv['UNIT_TEST_BIN'], src_name), 
                       [unit_obj, common_obj, test_obj[i]],
                       LINKFLAGS=linkflags,
                       LIBS=['boost_unit_test_framework', 'boost_system', 'boost_filesystem'] + casm_lib)
    if src_name[:-5] == "Structure":
      Clean(test,  Structure_out)
    if src_name[:-5] == "Clexulator":
      Clean(test,  Clexulator_out)
    if src_name[:-5] == "App":
      Depends(test, [casm_lib, casm])
      Clean(test,  App_out)
  
  elif src_name[:-5] == "Clexulator":
    test = lenv.Program(os.path.join(lenv['UNIT_TEST_BIN'], src_name), 
                       [unit_obj, common_obj, test_obj[i]],
                       LIBS=['boost_unit_test_framework', 'boost_system', 'boost_filesystem', 'dl'])
  else:
    test = lenv.Program(os.path.join(lenv['UNIT_TEST_BIN'], src_name), 
                       [unit_obj, test_obj[i]],
                       LIBS=['boost_unit_test_framework', 'boost_system', 'boost_filesystem'])
    
  # Execute 'scons Motif' or 'scons Structure', etc. to compile & run some unit tests
  if src_name[:-5] == "App":
    env.Alias(src_name[:-5], test, test[0].abspath + " --log_level=test_suite --catch_system_errors=no")
  else:
    env.Alias(src_name[:-5], test, test[0].abspath + " --log_level=test_suite")
  AlwaysBuild(test)
    
  if src_name[:-5] == "Clexulator":
    Clean(test, Clexulator_out)
  
  if src_name[:-5] == "Structure":
    Clean(test, Structure_out)
  
  if src_name[:-5] in COMMAND_LINE_TARGETS:
    env['IS_TEST'] = 1
