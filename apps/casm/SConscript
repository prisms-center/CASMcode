# http://www.scons.org/doc/production/HTML/scons-user.html
# This is: apps/casm

import os

Import('env', 'casm_lib', 'ccasm_lib', 'build_lib_paths', 'install_lib_paths')

# Build instructions
casm_include = env['CPPPATH'] + ['.']
libs = [env[x] for x in ['boost_system', 'boost_filesystem', 'boost_program_options', 'boost_regex', 'boost_chrono', 'dl', 'z']] + ['casm']

casm_obj = env.Object('casm.cpp', CPPPATH = casm_include)
Default(casm_obj)
Export('casm_obj')
env['COMPILE_TARGETS'] = env['COMPILE_TARGETS'] + [casm_obj]

linkflags = ""
if env['PLATFORM'] == 'darwin':
  linkflags = ['-rpath', '@loader_path/../lib']
else:
  linkflags = ['-Wl,--rpath=\\$$ORIGIN/../lib', '-z', 'origin']

# Build instructions
casm = env.Program(os.path.join(env['CASM_BIN'], "casm"),
                   [casm_obj],
                   LIBPATH=build_lib_paths,
                   LINKFLAGS=linkflags,
                   LIBS=libs)
Depends(casm, [casm_lib, ccasm_lib])
Default(casm)
env.Alias('casm', casm)
Export('casm')
env['COMPILE_TARGETS'] = env['COMPILE_TARGETS'] + [casm]

# Install instructions
casm_install = env.Install(os.path.join(env['PREFIX'],'bin'), casm)
env.Alias('casm_install', casm_install)
Export('casm_install')
env['INSTALL_TARGETS'] = env['INSTALL_TARGETS'] + [casm_install]

if 'casm_install' in COMMAND_LINE_TARGETS:
    env['IS_INSTALL'] = 1
